// Type [441a]

K<t> := FunctionField(Rationals());
R<x0,x1,x2,x3,x4,y,A,b,c,d0,d1,d2> := PolynomialRing(K,[1,1,1,1,1,1,2,2,2,1,1,1]);
P := Proj(R);

// Begin with [441a]I to [400]

// Defining type [441a]I
d := d0*x0+d1*x1+d2*x2;
Mat1 := -AntisymmetricMatrix(R,[b,c,x3,d,x1,x0,0,x0,x1,x2]);
I1 := Ideal(Pfaffians(Mat1,4));
Pf2 := MinimalBasis(I1)[2];
Pf3 := MinimalBasis(I1)[3];
Pf4 := MinimalBasis(I1)[4];
J1 := Ideal([x0,x1,x2]);

// Checking Betti table
q := MinimalBasis(ColonIdeal(I1,J1))[6] + A*y^2;
K1 := I1 + Ideal([x0*y,x1*y,x2*y,q]);
FreeResolution(GradedModule(K1));

// Defining type [400]
Q3 := x0^2 - x1^2 + x2*x3 + t^2*A;
F := b*x0 - x1*c + x3*d + t*y*A;
K2 := Ideal([Q3,F,x0*y-t*b,x1*y-t*c,x2*y-t*d,q]);
Pf2 in K2;
Pf3 in K2;
Pf4 in K2;

// Checking Betti table
FreeResolution(GradedModule(K2));

// Type [441a]II to type [420]

//Defining type [441a]II
Mat2 := -AntisymmetricMatrix(R,[b,c,x3,d,0,x0,0,x1,0,x2]);
M := (Pfaffians(Mat2,4));
I := Ideal(M);
Pf2 := M[3];
Pf4 := M[5];
J := Ideal([x0,x1,x2]);
q := MinimalBasis(ColonIdeal(I,J))[6]+c*y^2;
FreeResolution(GradedModule(I + Ideal([x0*y,x1*y,x2*y,q])));

// It deforms to type [420]
K3 := Ideal([M[1],M[2],x0*b+x3*d-t*c*y,x0*y+t*c,x1*y,x2*y]);
Pf2 in K3;
Pf4 in K3;
q in K3;
FreeResolution(GradedModule(K3));

X1 := Scheme(P,MinimalBasis(K1));
X2 := Scheme(P,MinimalBasis(K2));
X3 := Scheme(P,MinimalBasis(K3));
Dimension(X1);
Dimension(X2);
Dimension(X3);
