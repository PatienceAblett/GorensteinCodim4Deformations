// code used for proof of proposition 5.2

// it is easier for the computer if we calculate on the higher-dimensional
// family and then cut with a linear subspace afterwards

// first set up the deformation family over base field QQ(t)

K<t> := FunctionField(Rationals());

R<x0,x1,x2,x3,x4,x5,y0,y1,z0,z1,z2,w> := PolynomialRing(K,
[1,1,1,1,1,1,1,1,1,1,1,1]);
P := Proj(R);

// set values for parameters a,b
a:=1;
b:=1;

Mat1 := -AntisymmetricMatrix(R,
[x0,x1,x3,x2,x4,x5,y0,y1,0,0,y1,a*y0,b*y1,y0,z0,0,0,y0,y1,z1,z2]);
I := Ideal(Pfaffians(Mat1,6));
//MinimalBasis(I);

Q2 := x2*x3 - x1*x4 + x0*x5;
J := Ideal([y0,y1,Q2]);
K := ColonIdeal(I,J);
//MinimalBasis(K);

A := -a*y0^2 + a*x1*z1 + x0*z0 + x3*z2;
B := -b*x0*z1 + y0*y1 - x3*z1 - x1*z2;

// check Betti table is of type [210]a

L1 := K + Ideal([y0*w,y1*w,w*Q2]);
FreeResolution(GradedModule(L1));

// identify the general fibre and show that is is of type [200]
// by showing that it is bilinked to elliptic curve of degree 6

//t:=1; // use this if we want to examine a specific fibre

// check that residual family matches the one defined by
// equations in prop 5.2:

I2 := I + Ideal([y0*w+t*B,y1*w-t*A]);
J2 := Ideal([y0,y1]);
K2 := ColonIdeal(I2,J2);

F2 := t*x1*x2*y0*a - t*x1^2*y1*a - t*x0*x4*y0*b + t*x0*x2*y1*b + t*x0*x3*y1*b -
        t*x0*x1*y0 - t*x3*x4*y0 + t*x3^2*y1 - t*x0*x5*y1 - x2*x3*w + x1*x4*w -
        x0*x5*w;
L2 := K + Ideal([y0*w+t*B,y1*w-t*A,F2]);

L2 eq K2;


// compute the bilinkage of general fibre following [CGKK]

MB:=MinimalBasis(L2);
MB;

// T is a (2,2,3,3) complete intersection
// computer is sensitive to choices of cubics but this
// choice works in online calculator

T:=Ideal([
MB[1],MB[2],MB[7]+MB[8],MB[6]
]);

// first link:

G:=ColonIdeal(T,L2);

MG:=MinimalBasis(G);

MG;


// second link:

// S is a (2,2,2,3) complete intersection
// there is only a pencil of cubics so the choice of cubic
// is not really important
// there is no choice for the quadrics

S:=Ideal([
MG[1],MG[2],MG[3],7*MG[4]+3*MG[5]
]);

F:=ColonIdeal(S,G);
MinimalBasis(F);


// the bilinked ideal is F
// check that the result has Betti Table [200]

MinimalFreeResolution(GradedModule(F));
BettiTable($1);


// examine the bilinkage more closely:

K<t> := FunctionField(Rationals());
R<x0,x1,x3,y0,y1,z0,z1,z2,w>:=PolynomialRing(K,[1^^9]);

I:=Ideal([
    t*y1*z0 - t*y0*z1 + 7/3*t*z1^2 + t*y0*z2 - t*y1*z2 - 7/3*t*z2^2 + z0*w -
        z1*w,
    t*x3*z0 - t*x1*z1 + 7/3*t*y0*z1 + t*x1*z2 - t*x3*z2 - 7/3*t*y1*z2 - y0*w +
        y1*w,
    t*x0*z0 + t*x1*z0 - t*x0*z1 - t*x3*z1 + 7/3*t*y1*z1 - t*x1*z2 + t*x3*z2 -
        7/3*t*y0*z2 + y0*w - y1*w,
    t*y1^2 - t*x0*z1 - t*x1*z1 - t*x3*z2 + y1*w,
    t*y0*y1 - t*x0*z1 - t*x3*z1 - t*x1*z2 + y0*w,
    t*y0^2 + t*x1*z0 - t*x0*z1 - t*x1*z1 - t*x3*z1 + 7/3*t*y1*z1 - t*x1*z2 -
        7/3*t*y0*z2 + y0*w,
    x3*y0 - x1*y1 + 7/3*x0*z1,
    x0*y0 + x1*y0 - x0*y1 - x3*y1 - 7/3*x0*z2,
    t*x0*x1 + t*x1^2 - t*x0*x3 - t*x3^2 - 7/3*t*x0*y1 - 7/3*x0*w
]);


S:=Scheme(Proj(R),I);
IsIrreducible(S);
IsReduced(S);
Dimension(S);
Dimension(JacobianSubrankScheme(S));
Degree(S);


// thus by a Bertini argument, the intersection with a general 5-dimensional
// linear subspace is a normal elliptic curve of degree 6










// here is some older code identifying the general fibre of
// a 3-dimensional lifting of the deformation

// F is generated by 9 quadrics and we need to decide if
// it is coming from P2xP2 or P1xP1xP1

F:=ColonIdeal(S,G);
MinimalBasis(F);
IsPrime(F);


/////////////////////////////////////

// show that the resulting variety is 4 dimensional, irreducible, smooth
// so it cannot be P1xP1xP1


R<x0,x1,x3,y0,y1,z0,z1,z2,w>:=PolynomialRing(Rationals(),[1^^9]);
I:=Ideal([
    y1*z0 - y0*z1 + 7/3*z1^2 + y0*z2 - y1*z2 - 7/3*z2^2 + z0*w - z1*w,
    x3*z0 - x1*z1 + 7/3*y0*z1 + x1*z2 - x3*z2 - 7/3*y1*z2 - y0*w + y1*w,
    x0*z0 + x1*z0 - x0*z1 - x3*z1 + 7/3*y1*z1 - x1*z2 + x3*z2 - 7/3*y0*z2 + y0*w
        - y1*w,
    y1^2 - x0*z1 - x1*z1 - x3*z2 + y1*w,
    y0*y1 - x0*z1 - x3*z1 - x1*z2 + y0*w,
    y0^2 + x1*z0 - x0*z1 - x1*z1 - x3*z1 + 7/3*y1*z1 - x1*z2 - 7/3*y0*z2 + y0*w,
    x3*y0 - x1*y1 + 7/3*x0*z1,
    x0*y0 + x1*y0 - x0*y1 - x3*y1 - 7/3*x0*z2,
    x0*x1 + x1^2 - x0*x3 - x3^2 - 7/3*x0*y1 - 7/3*x0*w
]);

S:=Scheme(Proj(R),I);
IsIrreducible(S);
IsReduced(S);
Dimension(S);
Dimension(JacobianSubrankScheme(S));
