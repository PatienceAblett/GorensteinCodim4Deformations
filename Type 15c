// Magma code for Type [562]a

//[562]a to [550]

K<t> := FunctionField(Rationals());
R<x1,x2,x3,x4,y1,y2,a13,a23,b13,b23,a14,a24,b14,b24,H12,H34> := PolynomialRing(
K,[1,1,1,1,1,1,1,1,1,1,1,1,1,1,3,3]);
P := Proj(R);

F1 := a13*x1*x3+a23*x2*x3+a14*x1*x4+a24*x2*x4;
F2 := b13*x1*x3+b23*x2*x3+b14*x1*x4+b24*x2*x4;

F11 := a13*x3+a14*x4;
F12 := a23*x3+a24*x4;
F13 := a13*x1+a23*x2;
F14 := a14*x1+a24*x2;

F21 := b13*x3+b14*x4;
F22 := b23*x3+b24*x4;
F23 := b13*x1+b23*x2;
F24 := b14*x1+b24*x2;

// Type [562]aI
Mat01 := -AntisymmetricMatrix(R,[y2,0,0,F21,F11,x2,F22,F12,-x1,H34]);
I01 := Ideal(Pfaffians(Mat01,4));

Mat02 := -AntisymmetricMatrix(R,[y1,0,0,F23,F13,x4,F24,F14,-x3,H12]);
I02 := Ideal(Pfaffians(Mat02,4));

J0 := I01 + I02 + Ideal([y1*y2]);
FreeResolution(GradedModule(J0));

// Now the two matrices defining Ct

Mat11 := -AntisymmetricMatrix(R,[y2,t,0,F21,F11,x2,F22,F12,-x1,H12]);
I11 := Ideal(Pfaffians(Mat11,4));

Mat12 := -AntisymmetricMatrix(R,[y1,t,0,F23,F13,x4,F24,F14,-x3,H12]);
I12 := Ideal(Pfaffians(Mat12,4));

// The matrix defining the del Pezzo surface
Mat3 := -AntisymmetricMatrix(R,[y1,x2,-x1,t*a14,t*a24,-x3,t*a13,t*a23,x4,y2]);
K := Pfaffians(Mat3,4);

J1 := I11 + I12 + Ideal([y1*y2-t^2*(a14*a23-a13*a24)]);
MinimalBasis(J1);
// Note that J1 = K + general cubic

// J has 6 x 10 resolution as expected
FreeResolution(GradedModule(J1));

// Now consider [562]aII to [551]

Mat21 := -AntisymmetricMatrix(R,[y2,0,0,F21,F11,x2,F22,F12,-x1,H12]);
I21 := Ideal(Pfaffians(Mat21,4));

Mat22 := -AntisymmetricMatrix(R,[y1,t,0,F23,F13,x4,F24,F14,-x3,0]);
I22 := Ideal(Pfaffians(Mat22,4));

J2 := I21 + I22 + Ideal([y1*y2]);

FreeResolution(GradedModule(J2));

X0 := Scheme(P,MinimalBasis(J0));
X1 := Scheme(P,MinimalBasis(J1));
X2 := Scheme(P,MinimalBasis(J2));
Dimension(X0);
Dimension(X1);
Dimension(X2);
